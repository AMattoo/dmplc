package UAV_Utility_And_Criticality_Example
public

with Zero_Slack_Scheduling;

virtual processor flight_critical_partition
end flight_critical_partition;

virtual processor mission_critical_partition
end mission_critical_partition;

virtual processor mixed_utility_partition
	features
		degrade_mode: out event port;
		upgrade_mode: out event port;
end mixed_utility_partition;

processor mixed_criticality_and_utility_processor
end mixed_criticality_and_utility_processor;

thread actuation_task
	properties
		Period => 30 ms ;
		Compute_Execution_Time => 1 ms .. 1 ms ;
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis
		Zero_Slack_Scheduling::Zero_Slack_Instant => 30 ms ;
end actuation_task;

thread sensor_task
	properties
		Period => 65 ms ;
		Compute_Execution_Time => 100 us .. 100 us ;
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis
		Zero_Slack_Scheduling::Zero_Slack_Instant => 65 ms ;		
end sensor_task;

thread auxiliary_sensor_task
	properties
		Period => 50 ms ;
		Compute_Execution_Time => 500 us .. 500 us ;
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis
		Zero_Slack_Scheduling::Zero_Slack_Instant => 50 ms ;
end auxiliary_sensor_task;

process flight_critical_process
end flight_critical_process;

process implementation flight_critical_process.i
	subcomponents
		actuation: thread actuation_task;
		sensor: thread sensor_task;
		aux_sensor: thread auxiliary_sensor_task;
end flight_critical_process.i;

thread navigation_task
	properties
		Period => 50 ms ;
		Compute_Execution_Time => 11 ms .. 11 ms ;
		Zero_Slack_Scheduling::Utility => 10.0 ;
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis
		Zero_Slack_Scheduling::Zero_Slack_Instant => 49 ms ;
end navigation_task;

thread object_detection_task
	properties
		Period => 100 ms ;
		Compute_Execution_Time => 15 ms .. 40 ms ;
		Zero_Slack_Scheduling::Utility => 2.5 ;
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis
		Zero_Slack_Scheduling::Zero_Slack_Instant => 87 ms ;	
end object_detection_task;

thread multi_fidelity_video_streaming_task
	features
		degrade_mode: in event port;
		upgrade_mode: in event port;
end multi_fidelity_video_streaming_task;

thread implementation multi_fidelity_video_streaming_task.i
	modes
		high_fidelity_mode: initial mode;
		low_fidelity_mode: mode;
		high_fidelity_mode -[processor.degrade_mode]->low_fidelity_mode;
		low_fidelity_mode -[processor.upgrade_mode]->high_fidelity_mode; 
	properties
		Period => 120 ms in modes (low_fidelity_mode), 40 ms in modes (high_fidelity_mode);
		Compute_Execution_Time => 10 ms .. 10 ms in modes (low_fidelity_mode, high_fidelity_mode);
		Zero_Slack_Scheduling::Utility => 0.1 in modes (low_fidelity_mode), 0.5 in modes (high_fidelity_mode); 
		-- The zero slack instant is generated by the ZSQRAM scheduling analysis for each mode
		Zero_Slack_Scheduling::Zero_Slack_Instant => 120 ms in modes (low_fidelity_mode), 40 ms in modes (high_fidelity_mode);
end multi_fidelity_video_streaming_task.i;


process mixed_utility_video_streaming_process
end mixed_utility_video_streaming_process;

process implementation mixed_utility_video_streaming_process.i
	subcomponents
		mf_streaming_task: thread multi_fidelity_video_streaming_task.i ;
end mixed_utility_video_streaming_process.i;

process mission_critical_process
end mission_critical_process;

process implementation mission_critical_process.i
	subcomponents
		navigation: thread navigation_task;
		detection: thread object_detection_task;
end mission_critical_process.i;

system mixed_criticality_utility_system
end mixed_criticality_utility_system;

system implementation mixed_criticality_utility_system.i
	subcomponents
		flight_process: process flight_critical_process.i;
		mission_process: process mission_critical_process.i;
		mf_streaming_process: process mixed_utility_video_streaming_process.i;
		flight_partition: virtual processor flight_critical_partition;
		mission_partition: virtual processor mission_critical_partition;
		mixed_utility_partition: virtual processor mixed_utility_partition;
		mixed_criticality_utility_processor: processor mixed_criticality_and_utility_processor;
	properties
		-- Binding of processes to partitions
		Actual_Processor_Binding => (reference (flight_partition)) applies to flight_process;
		Actual_Processor_Binding => (reference (mission_partition)) applies to mission_process;
		Actual_Processor_Binding => (reference (mixed_utility_partition)) applies to mf_streaming_process;
		-- Binding of partitions to processor
		Actual_Processor_Binding => (reference (mixed_criticality_utility_processor)) applies to flight_partition;
		Actual_Processor_Binding => (reference (mixed_criticality_utility_processor)) applies to mission_partition;
		-- Note that the mixed-utility partition is assigned to the mission partition.
		-- This is recursively assigned to the mixed_criticality_utility_processor
		Actual_Processor_Binding => (reference (mission_partition)) applies to mixed_utility_partition;
		-- scheduling parameter
		Zero_Slack_Scheduling::Scheduling_Protocol => (ZSQRAM) applies to mixed_criticality_utility_processor;
		-- Assign the criticality of the partitions: lower number means higher criticality
		Zero_Slack_Scheduling::Criticality => 2 applies to flight_partition;
		Zero_Slack_Scheduling::Criticality => 1 applies to mission_partition;
		-- Assigns the partition-wise utility of the flight critical partition. 
		-- The mission partition has utility per mode and task
		Zero_Slack_Scheduling::Utility => 10.0 applies to flight_partition;
end mixed_criticality_utility_system.i;

end UAV_Utility_And_Criticality_Example;