package Dual_Criticality_Example
public
with Zero_Slack_Scheduling;

thread low_criticality_task
	properties
		Period => 4 ms ;
		Compute_Execution_Time => 2 ms .. 2 ms ;
		Zero_Slack_Scheduling::Zero_Slack_Instant => 4 ms ;
end low_criticality_task;

process low_criticality_process
end low_criticality_process;

process implementation low_criticality_process.i
	subcomponents
	  task: thread low_criticality_task;
end low_criticality_process.i;

thread high_criticality_task
	properties
		Period => 8 ms ;
		Compute_Execution_Time => 2 ms .. 5 ms ;
		Zero_Slack_Scheduling::Zero_Slack_Instant => 5 ms ;		
end high_criticality_task;

process high_criticality_process
end high_criticality_process;

process implementation high_criticality_process.i
	subcomponents
		task: thread high_criticality_task;
end high_criticality_process.i;

virtual processor high_criticality_partition
end high_criticality_partition;

virtual processor low_criticality_partition
end low_criticality_partition;

processor mixed_criticality_processor
end mixed_criticality_processor;

system mixed_criticality_system
end mixed_criticality_system;

system implementation mixed_criticality_system.i
	subcomponents
		mc_processor: processor mixed_criticality_processor;
		hc_partition: virtual processor high_criticality_partition;
		lc_partition: virtual processor low_criticality_partition;
		hc_process: process high_criticality_process.i;
		lc_process: process low_criticality_process.i;
	properties
		-- Assign the criticality of the partitions. Lower number means higher criticality
		Zero_Slack_Scheduling::Criticality => 1 applies to hc_partition;
		Zero_Slack_Scheduling::Criticality => 2 applies to lc_partition;
		Zero_Slack_Scheduling::Scheduling_Protocol => (ZSRM) applies to mc_processor;
		Actual_Processor_Binding => (reference (hc_partition)) applies to hc_process;
		Actual_Processor_Binding => (reference (lc_partition)) applies to lc_process;
		Actual_Processor_Binding => (reference (mc_processor)) applies to hc_partition;
		Actual_Processor_Binding => (reference (mc_processor)) applies to lc_partition;
end mixed_criticality_system.i;

end Dual_Criticality_Example;